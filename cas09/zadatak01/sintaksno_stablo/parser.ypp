%{
    #include <iostream>
    #include "SintaksnoStablo.hpp"
    #include "TablicaSimbola.hpp"

    #define YYDEBUG         (1)
    
    extern int yylex();

    void yyerror(const std::string& poruka) {
        std::cerr << poruka << std::endl;
        exit(EXIT_FAILURE);
    }

    TablicaSimbola tablica;

    ASTCvor* koren;
%}

%union {
    ASTCvor* c;
    double d;
    std::string* s;
}

%token SIN PROMENLJIVA
%token<s> ID 
%token<d> BROJ

%type<c> niz_naredbi naredba funkcija

%left '+' '-'
%left '*'
%right '['
%right '\''
%right UN_MINUS

%%

program: niz_naredbi {
    koren = $1;
}
;

niz_naredbi: niz_naredbi naredba '\n' {
    NizNaredbiCvor* niz = dynamic_cast<NizNaredbiCvor*>($1);
    niz->dodajNaredbu($2);
    $$ = niz;
}
| naredba '\n' {
    NizNaredbiCvor* niz = new NizNaredbiCvor();
    niz->dodajNaredbu($1);
    $$ = niz;
}
;

naredba: ID '=' funkcija {
        $$ = new DodelaCvor(*$1, $3);
        delete $1;
    }
    | funkcija {
        $$ = new IspisCvor($1);
    }
    | {
        $$ = new PrazanCvor();
    }
    ;

funkcija: SIN '(' funkcija ')' {
        $$ = new SinCvor($3);
    }
    | funkcija '[' BROJ ']' {
        $$ = new VrednostCvor($1, $3);
    }
    | funkcija '[' '-' BROJ ']' {
        $$ = new VrednostCvor($1, -$4);
    }
    | funkcija '+' funkcija {
        $$ = new SabiranjeCvor($1, $3);
    }
    | funkcija '-' funkcija {
        $$ = new OduzimanjeCvor($1, $3);
    }
    | funkcija '*' funkcija {
        $$ = new MnozenjeCvor($1, $3);
    }
    | funkcija '\'' {
        $$ = new IzvodCvor($1);
    }
    | '-' funkcija %prec UN_MINUS {
        $$ = new UnMinusCvor($2);
    }
    | ID '(' funkcija ')' {
        $$ = new KompozicijaCvor(*$1, $3);
        delete $1;
    }
    | ID {
        $$ = new PromenljivaCvor(*$1);
        delete $1;
    }
    | 'x' {
        $$ = new IdentitetCvor();
    }
    | BROJ {
        $$ = new KonstantaCvor($1);
    }
    ;

%%

int main() {
    //yydebug = 1;

    if (yyparse() == 0) {
        std::cout << "sve ok" << std::endl;
    } else {
        std::cout << "nije ok" << std::endl;
        return -1;
    }

    //std::cout << *koren << std::endl;
    koren->interpretiraj(tablica);
    delete koren;

    return 0;
}