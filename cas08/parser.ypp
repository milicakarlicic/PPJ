%{
    #include <iostream>
    #include <cstdlib>
    #include "SintaksnoStablo.hpp"

    extern int yylex();

    void yyerror(const std::string &poruka) {
        std::cerr << poruka << std::endl;
        exit(EXIT_FAILURE);
    }

    ASTCvor *koren = nullptr;

    TablicaSimbola tablica;
%}

%token DEF PRINT JEDNAKO
%token<d> BROJ
%token<s> ID

%type<c> izraz naredba niz_naredbi

%union {
    ASTCvor *c;
    double d;
    std::string *s;
}

%left JEDNAKO
%left '+'
%left '*'
%right UN_MINUS

%%

program: niz_naredbi {
    koren = $1;
}
;

niz_naredbi: niz_naredbi naredba ';' {
    NizNaredbi *niz = dynamic_cast<NizNaredbi*>($1);
    niz->dodajNaredbu($2);
    $$ = niz;
}
| naredba ';' {
    // niz_naredbi => niz_naredbi naredba ';' =>
    // naredba ; naredba ;
    NizNaredbi *niz = new NizNaredbi();
    niz->dodajNaredbu($1);
    $$ = niz;
}
;

naredba: DEF ID '=' izraz {
    $$ = new Definicija(*$2, $4);
    delete $2;
}
| PRINT '(' izraz ')' {
    $$ = new Ispis($3);
}
| ID '=' izraz {
    $$ = new Dodela(*$1, $3);
    delete $1;
}
| izraz JEDNAKO izraz {
    $$ = new Jednako($1, $3);
}
;

izraz: izraz '+' izraz {
    $$ = new Zbir($1, $3);
}
| izraz '*' izraz {
    $$ = new Proizvod($1, $3);
}
| '-' izraz %prec UN_MINUS {
    $$ = new Negacija($2);
}
| '(' izraz ')' {
    $$ = $2;
}
| BROJ {
    $$ = new Konstanta($1);
}
| ID {
    $$ = new Promenljiva(*$1);
    delete $1;
}
;

%%

int main() {
    if (yyparse() == 0) {
        std::cout << "sve ok" << std::endl;
    } else {
        std::cout << "nije ok" << std::endl;
        return -1;
    }

    // std::cout << *koren << std::endl;
    koren->interpretiraj(tablica);

    delete koren;

    return 0;
}