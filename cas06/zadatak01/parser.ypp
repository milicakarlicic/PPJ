
%{
    #include <iostream>
    #include <map>
    #include <vector>
    #include "Polinom.hpp"

    // za debagovanje sintaksne greske
    #define YYDEBUG     1

    using namespace std;

    extern int yylex();

    void yyerror(const string &poruka) {
        cerr << poruka << endl;
        exit(EXIT_FAILURE);
    }

    map<string, Polinom> promenljive;
%}

%left '+' '-'
%left '*'
%right UN_MINUS
%right '['
%right '\''

%token POLY PRINT DEG JEDNAKO RAZLICITO INT
%token<d> BROJ
%token<s> ID

%type<v> niz_brojeva
%type<p> polinom
%type<b> logicka_naredba

%union {
    double d;
    bool b;
    string *s;
    Polinom *p;
    vector<double> *v;
}

%%

niz_naredbi: niz_naredbi naredba ';'
    | naredba ';'
    ;

naredba: POLY ID '=' polinom {
        auto it = promenljive.find(*$2);
        if (it != promenljive.end()) {
            yyerror("Promenljiva vec definisana!");
        }
        promenljive[*$2] = *$4;
        delete $2; 
        delete $4;
    }
    | POLY ID {
        auto it = promenljive.find(*$2);
        if (it != promenljive.end()) {
            yyerror("Promenljiva vec definisana!");
        }
        promenljive[*$2] = Polinom();
        delete $2;
    }
    | POLY ID '(' BROJ ')' {
        auto it = promenljive.find(*$2);
        if (it != promenljive.end()) {
            yyerror("Promenljiva vec definisana!");
        }
        promenljive[*$2] = Polinom($4);
        delete $2;
    }
    | PRINT '(' polinom ')' {
        cout << *$3 << endl;
        delete $3;
    }
    | ID '=' polinom {
        // provera
        promenljive[*$1] = *$3;
        delete $1; 
        delete $3;
    }
    | logicka_naredba {
        if ($1) {
            cout << "true" << endl;
        } else {
            cout << "false" << endl;
        }
    }
    ;

logicka_naredba: polinom JEDNAKO polinom {
        $$ = *$1 == *$3;
        delete $1;
        delete $3;
    }
    | polinom RAZLICITO polinom {
        $$ = *$1 != *$3;
        delete $1;
        delete $3;
    }
    ;

polinom: polinom '\'' { 
        $$ = new Polinom($1->izvod());
        delete $1;
    }
    | INT '(' polinom ',' BROJ ')' {
        $$ = new Polinom($3->integral($5));
        delete $3;
    }
    | polinom '+' polinom {
        $$ = new Polinom(*$1 + *$3);
        delete $1; 
        delete $3;
    }
    | polinom '-' polinom {
        $$ = new Polinom(*$1 - *$3);
        delete $1; 
        delete $3;
    }
    | polinom '*' polinom {
        $$ = new Polinom(*$1 * *$3);
        delete $1; 
        delete $3;
    }
    | '-' polinom %prec UN_MINUS {
        $$ = new Polinom(-(*$2));
        delete $2;
    }  
    | polinom '[' BROJ ']' {
        $$ = new Polinom();
        $$->dodajKoef((*$1)[$3]);
        delete $1;
    }
    | BROJ {
        $$ = new Polinom();
        $$->dodajKoef($1);
    }
    | DEG '(' polinom ')'{
        $$ = new Polinom();
        $$->dodajKoef($3->getStepen());
        delete $3;
    }
    | '<' niz_brojeva '>' {
        $$ = new Polinom(*$2);
        delete $2;
    }
    | ID {
        auto it = promenljive.find(*$1);
        delete $1;
        if (it == promenljive.end()) {
            yyerror("Promenljiva nije definisana!");
        }
        $$ = new Polinom(it->second);
    }
    ;

niz_brojeva: niz_brojeva ',' BROJ { 
        $$ = $1;
        $$->push_back($3);
    }
    | BROJ {
        $$ = new vector<double>();
        $$->push_back($1);
    }
    ;

%%

int main() {
    // yydebug = 1;
    if (yyparse() == 0) {
        cout << "sve ok" << endl;
    }

    return 0;
}